buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.18.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        debug {
            keyAlias 'phonograph-debug'
            keyPassword 'debugkey'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'
        }
        release {
            keyAlias 'phonograph'
            storeFile file('../phonograph.keystore')
            storePassword 'kabouzeid'

            // "keystore.properties" file containing the release keys password is needed in order to sign the apk for release
            File propFile = project.rootProject.file('keystore.properties')
            if (propFile.exists()) {
                Properties properties = new Properties()
                properties.load(propFile.newDataInputStream())

                keyPassword properties.getProperty('keyPassword')
            } else {
                println 'keystore.properties not found'
                keyPassword null
            }
        }
    }
    compileSdkVersion 22
    buildToolsVersion '23.0.0 rc3'
    defaultConfig {
        applicationId "com.kabouzeid.gramophone"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 57
        versionName "0.9.41b dev-1"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.2.1@aar') {
        transitive = true;
    }
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:support-annotations:22.2.0'

    compile 'com.github.ksoichiro:android-observablescrollview:1.5.1'
    compile 'asia.ivity.android:drag-sort-listview:1.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.github.semoncat.seekarc:library:0.1'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'

    compile 'com.afollestad:material-dialogs:0.7.6.0'
    compile 'com.afollestad:material-cab:0.1.4'

    compile 'com.jakewharton:butterknife:6.1.0'
}
